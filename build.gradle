import org.apache.tools.ant.filters.*
import java.text.SimpleDateFormat

buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'org.ajoberstar:gradle-git:0.12.0'
  }
}

ext {
  repo = org.ajoberstar.grgit.Grgit.open(project.file('.'))
  build_rev = repo.head().id
  build_rev_short = build_rev.substring(0,7)
  build_timestamp =
    new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ssXXX").format(new Date())
}

subprojects {
  apply plugin: "java"
  apply plugin: "idea"

  group = "mappworks"
  version = "0-SNAPSHOT"
  sourceCompatibility = "1.8"

  repositories {
    mavenLocal()
    mavenCentral()
    maven {
      url "https://oss.sonatype.org/content/repositories/snapshots"
    }
  }

  configurations {
    provided
  }

  sourceSets {
    main {
      compileClasspath += configurations.provided
    }
  }

  idea {
    module {
      scopes.PROVIDED.plus += [configurations.provided]
    }
  }
}

project(":gasp-core") {
  processResources {
    filter ReplaceTokens, tokens: [
      "version": project.version,
      "revision": build_rev,
      "timestamp": build_timestamp
    ]
  }

  dependencies {
    compile ("org.springframework:spring-web:$springVersion") {
        exclude(group: 'commons-logging', module: 'commons-logging')
    }

    // spring
    compile "org.springframework:spring-jdbc:$springVersion"
    compile "org.springframework.security:spring-security-web:$springSecurityVersion"
    compile "org.springframework.security:spring-security-config:$springSecurityVersion"

    // database
    compile "com.zaxxer:HikariCP:2.2.5"
    //compile "com.impossibl.pgjdbc-ng:pgjdbc-ng:0.4"
    compile "org.postgresql:postgresql:9.3-1102-jdbc41"

    // spatial
    compile "com.vividsolutions:jts:1.13"


    // logging
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile "org.slf4j:jcl-over-slf4j:$slf4jVersion"
    // compile "org.slf4j:slf4j-log4j12:$slf4jVersion"
    compile "ch.qos.logback:logback-classic:1.1.2"

    // utility + miscellaneous
    compile "org.ini4j:ini4j:0.5.2"
    compile "org.jasypt:jasypt:1.9.2"
    compile "com.google.guava:guava:18.0"
    compile "com.fasterxml.jackson.core:jackson-databind:2.4.4"
    compile ("com.bedatadriven:jackson-datatype-jts:1.0-SNAPSHOT") {
      exclude(group: 'com.fasterxml.jackson.core', module: 'jackson-databind')
      exclude(group: 'com.vividsolutions', module: 'jts')
    }
    compile "org.ocpsoft.prettytime:prettytime:1.0.8.Final"

    provided "javax.servlet:javax.servlet-api:3.0.1"

    testCompile "junit:junit:4.12"
  }
}

project(":gasp-app") {
  apply plugin: "war"

  task explodeWar(type: Copy) {
    into "$buildDir/exploded"
    with war
  }

  war.dependsOn explodeWar
  war.archiveName = "gasp.war"

  dependencies {
    compile project(":gasp-core")
    compile "org.springframework:spring-webmvc:$springVersion"
    testCompile "junit:junit:4.12"
    testCompile "org.springframework:spring-test:$springVersion"
    testCompile "org.mockito:mockito-all:1.9.5"
    testCompile "org.eclipse.jetty:jetty-server:$jettyVersion"
    testCompile "org.eclipse.jetty:jetty-webapp:$jettyVersion"
    testCompile "org.eclipse.jetty:jetty-annotations:$jettyVersion"

    provided "javax.servlet:javax.servlet-api:3.1.0"
  }
}

project(":gasp-cli") {
  apply plugin: "application"

  applicationName = "gasp"
  mainClassName = "gasp.cli.GaspCLI"

  applicationDistribution.from(
    project(":gasp-app").buildDir.absolutePath + "/exploded"
  ) {
    into "war"
    include "**/*"
    exclude "**/*log4j*"
    exclude "**/slf4j*"
  }

  project.startScripts.with {
    doLast {
      unixScript.text = unixScript.text.replaceAll("-classpath",
        '-Dapp.home=\\$APP_HOME $0')
      windowsScript.text = windowsScript.text.replaceAll("-classpath",
        '-Dapp.home=%APP_HOME% $0')
    }
  }

  dependencies {
    compile project(":gasp-core")
    //compile 'jline:jline:2.10'
    compile 'com.beust:jcommander:1.35'
    compile "org.eclipse.jetty:jetty-server:$jettyVersion"
    compile "org.eclipse.jetty:jetty-webapp:$jettyVersion"
  }

  build.dependsOn(installApp)
}
